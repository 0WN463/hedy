start: program
program: command (" ")* ("\n"+ command (" ")*)* "\n"* //lines may end on spaces and might be separated by many newlines
command: "repeat " number " times " command -> repeat
         | "print " (quoted_text | list_access | var | sum) (" " (quoted_text | list_access | var | sum))*  -> print
         | "if " equality_check " " command " else " command -> ifelse
         | "if " equality_check " " command -> ifs //if cannot be used in Python, hence ifs
         | text " is ask " textwithspaces punctuation* -> ask
         | text " is " text ((", "|",") text)+  -> assign_list
         | text " is " sum  -> assign
         | text " is " var " at " (index | random) -> list_access_var
         | text " " textwithspaces -> invalid

ACCENTED_LETTER: "á" | "é" | "ó"

//TODO: I don't like the " " and "print " in the commands
//would rather have them here but I am unsure how to stop hem from appearing in the tree that way
//leaving as such for now

number: DIGIT+ -> number

equality_check: text " is " text (" and " equality_check)* -> equality_check
var: LETTER+ -> var //TODO: should be used in assign too?
list_access : var " at " (index | random) -> list_access

index : DIGIT+
random : "random"
punctuation : PUNCTUATION -> punctuation
textwithspaces: (LETTER | ACCENTED_LETTER | DIGIT | WS_INLINE)+ -> text
quoted_text: QUOTE (LETTER | ACCENTED_LETTER | DIGIT | PUNCTUATION | WS_INLINE)+ QUOTE -> text
QUOTE : "'"
text: (LETTER | ACCENTED_LETTER | DIGIT)+ -> text
PUNCTUATION: "!" | "?" | "." //uppercase places tokens in tree

?sum: product
   | sum " + " product -> addition
   | sum " - " product -> substraction

?product: atom
    | product " * " atom -> multiplication
    | product " / " atom -> division

?atom: number | text


%import common.SIGNED_INT -> INTEGER
%import common.LETTER   // imports from terminal library
%import common.DIGIT   // imports from terminal library
%import common.WS_INLINE   // imports from terminal library
%import common.NEWLINE   // imports from terminal library
%import common.CNAME -> NAME
